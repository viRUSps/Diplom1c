
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	СформироватьДвижения();
	
	РасчитатьОклад();
	
	СформироватьВзаиморасчеты();
	
КонецПроцедуры

Процедура СформироватьДвижения()	
	
	СформироватьДвиженияПоОкладу();
	
КонецПроцедуры

Процедура СформироватьДвиженияПоОкладу()
	
	Отбор = Новый Структура();
	Отбор.Вставить("ВидРасчета", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
	
	СтрокиОклада = Начисления.НайтиСтроки(Отбор);
	
	Если СтрокиОклада.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МинимальнаяДатаНачала = СтрокиОклада[0].ДатаНачала;
	МаксимальнаяДатаОкончания = СтрокиОклада[0].ДатаОкончания;
	
	Для Сч = 1 По СтрокиОклада.Количество() - 1 Цикл
		Если МинимальнаяДатаНачала > СтрокиОклада[Сч].ДатаНачала Тогда
			МинимальнаяДатаНачала = СтрокиОклада[Сч].ДатаНачала
		КонецЕсли;
		
		Если МаксимальнаяДатаОкончания < СтрокиОклада[Сч].ДатаОкончания Тогда 
			 МаксимальнаяДатаОкончания = СтрокиОклада[Сч].ДатаОкончания;
		КонецЕсли;
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_НачислениеЗарплатыНачисления.Сотрудник КАК Сотрудник,
	|	ВКМ_НачислениеЗарплатыНачисления.ВидРасчета КАК ВидРасчета,
	|	ВКМ_НачислениеЗарплатыНачисления.ДатаНачала КАК ДатаНачала,
	|	ВКМ_НачислениеЗарплатыНачисления.ДатаОкончания КАК ДатаОкончания,
	|	ВКМ_НачислениеЗарплатыНачисления.ГрафикРаботы КАК ГрафикРаботы,
	|	ВКМ_НачислениеЗарплатыНачисления.РазмерОклада
	|ПОМЕСТИТЬ ВТ_ДанныеДокумента
	|ИЗ
	|	Документ.ВКМ_НачислениеЗарплаты.Начисления КАК ВКМ_НачислениеЗарплатыНачисления
	|ГДЕ
	|	ВКМ_НачислениеЗарплатыНачисления.Ссылка = &Ссылка
	|	И ВКМ_НачислениеЗарплатыНачисления.ВидРасчета В (ЗНАЧЕНИЕ(ПланВидовРасчета.ВКМ_ОсновныеНачисления.Оклад),
	|		ЗНАЧЕНИЕ(ПланВидовРасчета.ВКМ_ОсновныеНачисления.Отпускные))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.Период КАК Период,
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник КАК Сотрудник,
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.Оклад КАК Оклад
	|ПОМЕСТИТЬ ВТ_ИсторияОклада
	|ИЗ
	|	РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&МинимальнаяДатаНачала, Сотрудник В
	|		(ВЫБРАТЬ
	|			ВТ_ДанныеДокумента.Сотрудник КАК Сотрудник
	|		ИЗ
	|			ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента)) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УсловияОплатыСотрудников.Период,
	|	УсловияОплатыСотрудников.Сотрудник,
	|	УсловияОплатыСотрудников.Оклад
	|ИЗ
	|	РегистрСведений.ВКМ_УсловияОплатыСотрудников КАК УсловияОплатыСотрудников
	|ГДЕ
	|	УсловияОплатыСотрудников.Период > &МинимальнаяДатаНачала
	|	И УсловияОплатыСотрудников.Период < &МаксимальнаяДатаОкончания
	|	И УсловияОплатыСотрудников.Сотрудник В
	|		(ВЫБРАТЬ
	|			ВТ_ДанныеДокумента.Сотрудник КАК Сотрудник
	|		ИЗ
	|			ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТекущаяЗапись.Период КАК ДатаНачала,
	|	ТекущаяЗапись.Сотрудник КАК Сотрудник,
	|	ЕСТЬNULL(ДОБАВИТЬКДАТЕ(МИНИМУМ(СледующаяЗапись.Период), ДЕНЬ, -1), ДАТАВРЕМЯ(3999, 12, 21)) КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТ_Интервалы
	|ИЗ
	|	ВТ_ИсторияОклада КАК ТекущаяЗапись
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИсторияОклада КАК СледующаяЗапись
	|		ПО ТекущаяЗапись.Сотрудник = СледующаяЗапись.Сотрудник
	|		И ТекущаяЗапись.Период < СледующаяЗапись.Период
	|СГРУППИРОВАТЬ ПО
	|	ТекущаяЗапись.Сотрудник,
	|	ТекущаяЗапись.Период
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Интервалы.Сотрудник КАК Сотрудник,
	|	ВТ_ИсторияОклада.Оклад КАК Оклад,
	|	ВТ_Интервалы.ДатаНачала КАК ДатаНачала,
	|	ВТ_Интервалы.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТ_ИнтервалыОклада
	|ИЗ
	|	ВТ_Интервалы КАК ВТ_Интервалы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИсторияОклада КАК ВТ_ИсторияОклада
	|		ПО ВТ_Интервалы.Сотрудник = ВТ_ИсторияОклада.Сотрудник
	|		И ВТ_Интервалы.ДатаНачала = ВТ_ИсторияОклада.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДокумента.Сотрудник КАК Сотрудник,
	|	ВТ_ДанныеДокумента.ВидРасчета КАК ВидРасчета,
	|	ВЫБОР
	|		КОГДА ВТ_ДанныеДокумента.ДатаНачала < ВТ_ИнтервалыОклада.ДатаНачала
	|			ТОГДА ВТ_ИнтервалыОклада.ДатаНачала
	|		ИНАЧЕ ВТ_ДанныеДокумента.ДатаНачала
	|	КОНЕЦ КАК ПериодДействияНачало,
	|	ВЫБОР
	|		КОГДА ВТ_ДанныеДокумента.ДатаОкончания > ВТ_ИнтервалыОклада.ДатаОкончания
	|			ТОГДА ВТ_ИнтервалыОклада.ДатаОкончания
	|		ИНАЧЕ ВТ_ДанныеДокумента.ДатаОкончания
	|	КОНЕЦ КАК ПериодДействияКонец,
	|	ВТ_ДанныеДокумента.ГрафикРаботы КАК ГрафикРаботы,
	|	ВТ_ИнтервалыОклада.Оклад КАК Показатель,
	|	ВТ_ДанныеДокумента.РазмерОклада
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИнтервалыОклада КАК ВТ_ИнтервалыОклада
	|		ПО ВТ_ДанныеДокумента.Сотрудник = ВТ_ИнтервалыОклада.Сотрудник
	|		И ВТ_ДанныеДокумента.ДатаНачала <= ВТ_ИнтервалыОклада.ДатаОкончания
	|		И ВТ_ДанныеДокумента.ДатаОкончания >= ВТ_ИнтервалыОклада.ДатаНачала
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НачислениеЗарплатыНачисления.Сотрудник,
	|	НачислениеЗарплатыНачисления.ВидРасчета,
	|	НачислениеЗарплатыНачисления.ДатаНачала,
	|	НачислениеЗарплатыНачисления.ДатаОкончания,
	|	НачислениеЗарплатыНачисления.ГрафикРаботы,
	|	NULL,
	|	NULL
	|ИЗ
	|	Документ.ВКМ_НачислениеЗарплаты.Начисления КАК НачислениеЗарплатыНачисления
	|ГДЕ
	|	НачислениеЗарплатыНачисления.Ссылка = &Ссылка
	|	И НачислениеЗарплатыНачисления.ВидРасчета ССЫЛКА ПланВидовРасчета.ВКМ_ОсновныеНачисления
	|	И НачислениеЗарплатыНачисления.ВидРасчета <> ЗНАЧЕНИЕ(ПланВидовРасчета.ВКМ_ОсновныеНачисления.Оклад)
	|	И НачислениеЗарплатыНачисления.ВидРасчета <> ЗНАЧЕНИЕ(ПланВидовРасчета.ВКМ_ОсновныеНачисления.Отпускные)";
		
		
		
		
	
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МинимальнаяДатаНачала", МинимальнаяДатаНачала);
	Запрос.УстановитьПараметр("МаксимальнаяДатаОкончания", МаксимальнаяДатаОкончания);

	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
		Движение.ПериодРегистрации = Дата;
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	 
	
	
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать(); 
	

   
  
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	//Данный фрагмент построен конструктором.
	//При повторном использовании конструктора, внесенные вручную данные будут утеряны!
	//
	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	
КонецПроцедуры


Процедура РасчитатьОклад()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_ДанныеГрафика.НомерСтроки КАК НомерСтроки,
	|	ВКМ_ДанныеГрафика.ЗначениеПериодДействия КАК Норма,
	|	ВКМ_ДанныеГрафика.ЗначениеФактическийПериодДействия КАК Факт
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(Регистратор = &Ссылка) КАК ВКМ_ДанныеГрафика";

	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Движение = Движения.ВКМ_ОсновныеНачисления.Получить(ВыборкаДетальныеЗаписи.НомерСтроки - 1);
		
		Если ВыборкаДетальныеЗаписи.Норма > 0 Тогда
            Движение.Результат = (Движение.РазмерОклада / ВыборкаДетальныеЗаписи.Норма) * ВыборкаДетальныеЗаписи.Факт;
        Иначе
            Движение.Результат = 0;
        КонецЕсли;
		
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать(, Истина);
	
КонецПроцедуры

Процедура СформироватьВзаиморасчеты()
	
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	
	Движения.ВКМ_ОсновныеНачисления.Записать();
    Движения.ВКМ_ДополнительныеНачисления.Записать();
    Движения.ВКМ_Удержания.Записать();
   
    Запрос = Новый Запрос;
    Запрос.Текст = 
    "ВЫБРАТЬ
    |	ВТ_Данные.Сотрудник КАК Сотрудник,
    |	СУММА(ВТ_Данные.Результат) КАК СуммаИтог
    |ИЗ
    |	(ВЫБРАТЬ
    |		Сотрудник,
    |		Результат,
    |		Регистратор
    |	ИЗ
    |		РегистрРасчета.ВКМ_ОсновныеНачисления
    |
    |	ОБЪЕДИНИТЬ ВСЕ
    |
    |	ВЫБРАТЬ
    |		Сотрудник,
    |		Результат,
    |		Регистратор
    |	ИЗ
    |		РегистрРасчета.ВКМ_ДополнительныеНачисления
    |
    |	ОБЪЕДИНИТЬ ВСЕ
    |
    |	ВЫБРАТЬ
    |		Сотрудник,
    |		-Результат,
    |		Регистратор
    |	ИЗ
    |		РегистрРасчета.ВКМ_Удержания) КАК ВТ_Данные
    |ГДЕ
    |	ВТ_Данные.Регистратор = &Ссылка
    |СГРУППИРОВАТЬ ПО
    |	ВТ_Данные.Сотрудник";
    
    Запрос.УстановитьПараметр("Ссылка", Ссылка);
    
    РезультатЗапроса = Запрос.Выполнить();
    Выборка = РезультатЗапроса.Выбрать();
    
    Пока Выборка.Следующий() Цикл
  
        Если Выборка.СуммаИтог = 0 Тогда
            Продолжить;
        КонецЕсли;
        
        Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
        Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
        Движение.Период      = Дата;
        Движение.Сотрудник   = Выборка.Сотрудник;
        Движение.Сумма       = Выборка.СуммаИтог;
    КонецЦикла;
    
    Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать();
    
КонецПроцедуры




